PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>

SELECT
  ?number_of_works
  ?author ?authorLabel (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WITH {
  SELECT DISTINCT ?author WHERE {
    ?author ( wdt:P108 | wdt:P1416 ) / wdt:P17 target1: .
  }
} AS %authors
WITH {
  SELECT
    ?author
    (COUNT(DISTINCT ?work) AS ?number_of_works)
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    INCLUDE %authors
    ?work wdt:P50 ?author .
    ?work wdt:P921 / (wdt:P361+ | wdt:P1269+ | (wdt:P31* / wdt:P279*) ) target2: .
  }
  GROUP BY ?author
} AS %results
WHERE {
  INCLUDE %results
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?number_of_works)
