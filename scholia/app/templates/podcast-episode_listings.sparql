PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

# title: listings for this podcast
SELECT
  ?Identifier ?IdentifierLabel
  ?value (SAMPLE(?idUrls) as ?valueUrl)
  ?IdentifierDescription
WITH {
  SELECT ?Identifier ?Value ?formatterurl WHERE {
    target: ?IDdir ?Value .
    ?Identifier wikibase:directClaim ?IDdir ;
            wdt:P31 wd:Q84574746 .
    OPTIONAL { 
      ?Identifier wdt:P1630 ?formatterurl .
    }
  } LIMIT 500
} AS %RESULTS {
  { SELECT * WHERE {
      INCLUDE %RESULTS
      BIND(IRI(REPLACE(?formatterurl, '\\\\$1', ENCODE_FOR_URI(str(?Value)))) AS ?idUrls).
    }
  } UNION {
    SELECT * WHERE {
      INCLUDE %RESULTS
      BIND(IRI(REPLACE(?formatterurl, '\\\\$1', str(?Value))) AS ?idUrls).
    }
  }
  BIND(CONCAT(?Value, " â†—") AS ?value)
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
}
GROUP BY ?Identifier ?IdentifierLabel ?IdentifierDescription ?value
ORDER BY ASC(?IdentifierLabel)
