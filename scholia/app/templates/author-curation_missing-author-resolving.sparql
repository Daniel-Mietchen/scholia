#title: Variants of author name strings
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT 
  (COUNT(?work) AS ?count)
  ?string
  (CONCAT("https://author-disambiguator.toolforge.org/names_oauth.php?doit=Look+for+author&name=", 
        ENCODE_FOR_URI(?string)) AS ?author_resolver_url) 
WITH
{
  # Find strings associated with the target author
  SELECT DISTINCT ?string_
  WHERE
  {
    { target: rdfs:label ?string_. } # in label
    UNION
    { target: skos:altLabel ?string_. } # in alias
    UNION
    {
      ?author_statement ps:P50 target: ; 
                        pq:P1932 ?string_. # in "stated as" strings for "author" statements on work items
    }
  }
} AS %RAWstrings
WITH
# This part is due to Dipsacus fullonum, as per https://w.wiki/5Brk
{
  # Calculate capitalization variants of these raw strings
  SELECT DISTINCT ?string
  WHERE
  {
    {
      INCLUDE %RAWstrings
      BIND(STR(?string_) AS ?string) # the raw strings
    }
    UNION
    {
      INCLUDE %RAWstrings
      BIND(UCASE(STR(?string_)) AS ?string) # uppercased versions of the raw strings
    }
    UNION
    {
      INCLUDE %RAWstrings
      BIND(LCASE(STR(?string_)) AS ?string) # lowercased versions of the raw strings
    }
  }
} AS %NORMALIZEDstrings
WHERE {
  # Find works that have "author name string" values equal to these normalized strings
  INCLUDE %NORMALIZEDstrings
  OPTIONAL { ?work wdt:P2093 ?string. }
}
GROUP BY ?string
ORDER BY DESC (?count)
