PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>

SELECT DISTINCT
  ?publication_date
  ?work ?workLabel (CONCAT("/work/", SUBSTR(STR(?work), 32)) AS ?workUrl)
WITH {
  SELECT DISTINCT ?researcher WHERE {
    ?researcher ( wdt:P108 | wdt:P463 | wdt:P1416 ) / wdt:P361* target1: .
  } 
} AS %researchers
WHERE {
  INCLUDE %researchers

  ?work wdt:P50 ?researcher ;
        wdt:P4510 target2: .
  OPTIONAL {
    ?work wdt:P577 ?publication_datetime .
    BIND(xsd:date(?publication_datetime) AS ?publication_date)
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en,da,de,es,fr,jp,nl,no,ru,sv,zh". }
}
ORDER BY DESC(?publication_date)
